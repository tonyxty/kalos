program = { SOI ~ toplevel* ~ EOI }

toplevel = _{ def }
def = { "def" ~ identifier ~ "(" ~ param_list ~ ")" ~ stmt }
param_list = { identifier? ~ ("," ~ identifier)* }

stmt = _{ compound_stmt | var_stmt | assignment_stmt | return_stmt | if_stmt | while_stmt | expr_stmt }
compound_stmt = { "{" ~ stmt* ~ "}" }
var_stmt = { "var" ~ identifier ~ (":" ~ type_expr)? ~ ("=" ~ expr)? ~ ";" }
assignment_stmt = { expr ~ "=" ~ expr ~ ";" }
return_stmt = { "return" ~ expr ~ ";" }
if_stmt = { "if" ~ "(" ~ expr ~ ")" ~ stmt ~ ("else" ~ stmt)? }
while_stmt = { "while" ~ "(" ~ expr ~ ")" ~ stmt }
expr_stmt = { expr ~ ";" }

expr = { call | atom ~ (operator ~ atom)* }
atom = _{ "(" ~ expr ~ ")" | literal | identifier }
operator = _{ power | multiply | divide | modulo | add | subtract }
	add = { "+" }
	subtract = { "-" }
	multiply = { "*" }
	divide = { "/" }
	power = { "**" }
	modulo = { "%" }

call = { atom ~ "(" ~ arg_list ~ ")" }
arg_list = { expr? ~ ("," ~ expr)* }

type_expr = { "auto" }

literal = @{ ASCII_DIGIT+ }
identifier = @{ LETTER ~ (LETTER | NUMBER | "_")* }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
