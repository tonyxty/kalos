program = _{ SOI ~ toplevel* ~ EOI }

toplevel = _{ def }
def = { "def" ~ prototype ~ ("extern" ~ ";" | compound_stmt) }

prototype = { identifier ~ "(" ~ param_list ~ ellipsis? ~ ")" ~ ("->" ~ type_expr)? }
param_list = { identifier? ~ ("," ~ identifier)* }
ellipsis = { "..." }

stmt = _{ compound_stmt | var_stmt | assignment_stmt | return_stmt | if_stmt | while_stmt | expr_stmt }
compound_stmt = { "{" ~ stmt* ~ "}" }
var_stmt = { "var" ~ identifier ~ (":" ~ type_expr)? ~ ("=" ~ expr)? ~ ";" }
assignment_stmt = { expr ~ "=" ~ expr ~ ";" }
return_stmt = { "return" ~ expr? ~ ";" }
if_stmt = { "if" ~ "(" ~ expr ~ ")" ~ stmt ~ ("else" ~ stmt)? }
while_stmt = { "while" ~ "(" ~ expr ~ ")" ~ stmt }
expr_stmt = { expr ~ ";" }

expr = { primary ~ (operator ~ primary)* }
primary = _{ call | atom }
atom = _{ "(" ~ expr ~ ")" | literal | identifier }

call = { atom ~ "(" ~ arg_list ~ ")" }
arg_list = { expr? ~ ("," ~ expr)* }
operator = _{ power | multiply | divide | modulo | add | subtract }
	add = { "+" }
	subtract = { "-" }
	multiply = { "*" }
	divide = { "/" }
	power = { "**" }
	modulo = { "%" }

type_expr = { auto | int }
    auto = { "auto" }
    int = { "int" }

literal = @{ ASCII_DIGIT+ }
identifier = @{ LETTER ~ (LETTER | NUMBER | "_")* }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
